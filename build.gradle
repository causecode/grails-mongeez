buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://repo.grails.org/grails/core" }
        maven {
            credentials {
                username mavenUser
                password mavenPassword
            }
            url "https://nexus.causecode.com/repository/maven-releases/"
        }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.causecode.plugins:gradle-code-quality:1.0.0"
    }
}

version "1.0.1"
group "com.causecode.plugins"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.grails-plugin-publish"
apply plugin:"org.grails.grails-gsp"
apply plugin: 'gradle-code-quality'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
    maven {
        credentials {
            username mavenUser
            password mavenPassword
        }
        url "https://nexus.causecode.com/repository/maven-releases/"
    }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    // For Gradle Plugin
    compile gradleApi()

    provided "org.springframework.boot:spring-boot-starter-logging"
    provided "org.springframework.boot:spring-boot-autoconfigure"
    provided "org.springframework.boot:spring-boot-starter-tomcat"

    compile "org.grails:grails-core:3.3.5"
    compile "org.grails:grails-web-boot:3.3.5"
    compile "org.grails:grails-plugin-databinding:3.3.5"
    compile "org.grails:grails-plugin-services:3.3.5"
    compile "org.grails:grails-plugin-url-mappings:3.3.5"
    compile "org.grails.plugins:scaffolding:3.4.1"
    compile "org.grails.plugins:gsp:3.3.1"

    provided "org.grails.plugins:mongodb"

    compile "org.mongeez:mongeez:0.9.7-RC1"
    compile "org.reflections:reflections:0.9.10"

    profile "org.grails.profiles:web-plugin"

    provided "org.grails:grails-plugin-domain-class"

    testCompile "org.grails:grails-test-mixins:3.3.0"
}

project.publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java

            artifactId "grails-mongeez"

            pom.withXml {
                def pomNode = asNode()
                // version-less dependencies are handled with dependencyManagement
                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    it.replaceNode {}
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username mavenPublishUser
                password mavenPublishPassword
            }
            url "https://nexus.causecode.com/repository/maven-releases/"
        }
    }
}

grailsPublish {
    // TODO: Provide values here
    githubSlug = 'causecode/grails-mongeez'
	repo = 'os'
    license {
        name = 'Apache-2.0'
    }
    title = "Grails Mongeez"
    desc = "Grails Mongeez plugin for Grails 3"
    developers = [causecode:"CauseCode Technologies Pvt Ltd"]
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}
